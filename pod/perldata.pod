=head1 NAME

perldata - Perl data structures

=head1 DESCRIPTION

=head2 Variable names

I<Perl> has three data structures: scalars, arrays of scalars, and
associative arrays of scalars, known as "hashes".  Normal arrays are
indexed by number, and hash arrays by string.

Scalar values are always named with '$', even when referring to a scalar
that is part of an array.  It works like the English word "the".  Thus
we have:

    $days		# the simple scalar value "days"
    $days[28]		# the 29th element of array @days
    $days{'Feb'}	# the 'Feb' value from hash %days
    $#days		# the last index of array @days

but entire arrays or array slices are denoted by '@', which works much like
the word "these" or "those":

    @days		# ($days[0], $days[1],... $days[n])
    @days[3,4,5]	# same as @days[3..5]
    @days{'a','c'}	# same as ($days{'a'},$days{'c'})

and entire hashes are denoted by '%':

    %days		# (key1, val1, key2, val2 ...)

In addition, subroutines are named with an initial '&', though this is
optional when it's otherwise unambiguous (just as "do" is often
redundant in English).  Symbol table entries can be named with an
initial '*', but you don't really care about that yet.

Every variable type has its own namespace.  You can, without fear of
conflict, use the same name for a scalar variable, an array, or a hash
(or, for that matter, a filehandle, a subroutine name, or a label).
This means that $foo and @foo are two different variables.  It also
means that $foo[1] is a part of @foo, not a part of $foo.  This may
seem a bit weird, but that's okay, because it is weird.

Since variable and array references always start with '$', '@', or '%',
the "reserved" words aren't in fact reserved with respect to variable
names.  (They ARE reserved with respect to labels and filehandles,
however, which don't have an initial special character.  You can't have
a filehandle named "log", for instance.  Hint: you could say
open(LOG,'logfile') rather than open(log,'logfile').  Using uppercase
filehandles also improves readability and protects you from conflict
with future reserved words.)  Case IS significant--"FOO", "Foo" and
"foo" are all different names.  Names that start with a letter or
underscore may also contain digits and underscores.

It is possible to replace such an alphanumeric name with an expression
that returns a reference to an object of that type.  For a description
of this, see L<perlref>.

Names that start with a digit may only contain more digits.  Names
which do not start with a letter, underscore,  or digit are limited to
one character, e.g.  "$%" or "$$".  (Most of these one character names
have a predefined significance to I<perl>.  For instance, $$ is the
current process id.)

=head2 Context

The interpretation of operations and values in perl sometimes depends
on the requirements of the context around the operation or value.
There are two major contexts: scalar and list.  Certain operations
return list values in contexts wanting a list, and scalar values
otherwise.  (If this is true of an operation it will be mentioned in
the documentation for that operation.)  In other words, Perl overloads
certain operations based on whether the expected return value is
singular or plural.  (Some words in English work this way, like "fish"
and "sheep".)

In a reciprocal fashion, an operation provides either a scalar or a
list context to each of its arguments.  For example, if you say

    int( <STDIN> )

the integer operation provides a scalar context for the <STDIN>
operator, which responds by reading one line from STDIN and passing it
back to the integer operation, which will then find the integer value
of that line and return that.  If, on the other hand, you say

    sort( <STDIN> )

then the sort operation provides a list context for <STDIN>, which
will proceed to read every line available up to the end of file, and
pass that list of lines back to the sort routine, which will then
sort those lines and return them as a list to whatever the context
of the sort was.

Assignment is a little bit special in that it uses its left argument to
determine the context for the right argument.  Assignment to a scalar
evaluates the righthand side in a scalar context, while assignment to
an array or array slice evaluates the righthand side in a list
context.  Assignment to a list also evaluates the righthand side in a
list context.

User defined subroutines may choose to care whether they are being
called in a scalar or list context, but most subroutines do not
need to care, because scalars are automatically interpolated into
lists.  More on that later.

=head2 Scalar values

Scalar variables may contain various kinds of singular data, such as
numbers, strings and references.  In general, conversion from one form
to another is transparent.  (A scalar may not contain multiple values,
but may contain a reference to an array or hash containing multiple
values.)  Because of the automatic conversion of scalars, operations and
functions that return scalars don't need to care (and, in fact, can't
care) whether the context is looking for a string or a number.

A scalar value is interpreted as TRUE in the Boolean sense if it is not
the null string or the number 0 (or its string equivalent, "0").  The
Boolean context is just a special kind of scalar context.

There are actually two varieties of null string: defined and
undefined.  Undefined null strings are returned when there is no real
value for something, such as when there was an error, or at end of
file, or when you refer to an uninitialized variable or element of an
array.  An undefined null string may become defined the first time you
use it as if it were defined, but prior to that you can use the
defined() operator to determine whether the value is defined or not.

The length of an array is a scalar value.  You may find the length of
array @days by evaluating "$#days", as in I<csh>.  (Actually, it's not
the length of the array, it's the subscript of the last element, since
there is (ordinarily) a 0th element.)  Assigning to $#days changes the
length of the array.  Shortening an array by this method destroys
intervening values.  Lengthening an array that was previously shortened
no longer recovers the values that were in those elements.  (It used to
in Perl 4, but we had to break this make to make sure destructors were
called when expected.)  You can also gain some measure of efficiency by
preextending an array that is going to get big.  (You can also extend
an array by assigning to an element that is off the end of the array.)
You can truncate an array down to nothing by assigning the null list ()
to it.  The following are equivalent:

    @whatever = ();
    $#whatever = $[ - 1;

If you evaluate a named array in a scalar context, it returns the length of
the array.  (Note that this is not true of lists, which return the
last value, like the C comma operator.)  The following is always true:

    scalar(@whatever) == $#whatever - $[ + 1;

The semantics of $[ are changed in Perl 5 such that any file that
doesn't set the value of $[ doesn't need to worry about it.  (It's more
or less deprecated, in other words.)  So in general you can just assume
that

    scalar(@whatever) == $#whatever + 1;

If you evaluate a hash in a scalar context, it returns a value which is
true if and only if the hash contains any key/value pairs.  (If there
are any key/value pairs, the value returned is a string consisting of
the number of used buckets and the number of allocated buckets,
separated by a slash.)

=head2 Scalar literals

Numeric literals are specified in any of the usual floating point or
integer formats:


    12345
    12345.67
    .23E-10
    0xffff	# hex
    0377	# octal
    4_294_967_296

String literals are delimited by either single or double quotes.  They
work much like shell quotes:  double-quoted string literals are subject
to backslash and variable substitution; single-quoted strings are not
(except for \' and \\).  The usual backslash rules apply for making
characters such as newline, tab, etc., as well as some more exotic
forms:

    \t		tab
    \n		newline
    \r		return
    \f		form feed
    \v		vertical tab, whatever that is
    \b		backspace
    \a		alarm (bell)
    \e		escape
    \033	octal char
    \x1b	hex char
    \c[		control char
    \l		lowercase next char
    \u		uppercase next char
    \L		lowercase till \E
    \U		uppercase till \E
    \E		end case modification
    \Q		quote regexp metacharacters till \E

You can also embed newlines directly in your strings, i.e. they can end
on a different line than they begin.  This is nice, but if you forget
your trailing quote, the error will not be reported until I<perl> finds
another line containing the quote character, which may be much further
on in the script.  Variable substitution inside strings is limited to
scalar variables, arrays, and array slices.  (In other words,
identifiers beginning with $ or @, followed by an optional bracketed
expression as a subscript.)  The following code segment prints out "The
price is $100."


    $Price = '$100';	# not interpreted
    print "The price is $Price.\n";	# interpreted

Note that you can put curly brackets around the identifier to delimit
it from following alphanumerics.  Also note that a single quoted string
must be separated from a preceding word by a space, since single quote
is a valid character in an identifier (see Packages).

Two special literals are __LINE__ and __FILE__, which represent the
current line number and filename at that point in your program.  They
may only be used as separate tokens; they will not be interpolated into
strings.  In addition, the token __END__ may be used to indicate the
logical end of the script before the actual end of file.  Any following
text is ignored, but may be read via the DATA filehandle.  (The DATA
filehandle may read data only from the main script, but not from any
required file or evaluated string.)  The two control characters ^D and
^Z are synonyms for __END__.

A word that doesn't have any other interpretation in the grammar will
be treated as if it had single quotes around it.  For this purpose, a
word consists only of alphanumeric characters and underline, and must
start with an alphabetic character.  As with filehandles and labels, a
bare word that consists entirely of lowercase letters risks conflict
with future reserved words, and if you use the B<-w> switch, Perl will
warn you about any such words.

Array variables are interpolated into double-quoted strings by joining all
the elements of the array with the delimiter specified in the $"
variable, space by default.  The following are equivalent:

    $temp = join($",@ARGV);
    system "echo $temp";

    system "echo @ARGV";

Within search patterns (which also undergo double-quotish substitution)
there is a bad ambiguity:  Is /$foo[bar]/ to be interpreted as
/${foo}[bar]/ (where [bar] is a character class for the regular
expression) or as /${foo[bar]}/ (where [bar] is the subscript to array
@foo)?  If @foo doesn't otherwise exist, then it's obviously a
character class.  If @foo exists, perl takes a good guess about [bar],
and is almost always right.  If it does guess wrong, or if you're just
plain paranoid, you can force the correct interpretation with curly
brackets as above.

A line-oriented form of quoting is based on the shell here-is syntax.
Following a << you specify a string to terminate the quoted material,
and all lines following the current line down to the terminating string
are the value of the item.  The terminating string may be either an
identifier (a word), or some quoted text.  If quoted, the type of
quotes you use determines the treatment of the text, just as in regular
quoting.  An unquoted identifier works like double quotes.  There must
be no space between the << and the identifier.  (If you put a space it
will be treated as a null identifier, which is valid, and matches the
first blank line--see Merry Christmas example below.)  The terminating
string must appear by itself (unquoted and with no surrounding
whitespace) on the terminating line.

	print <<EOF;		# same as above
    The price is $Price.
    EOF

	print <<"EOF";		# same as above
    The price is $Price.
    EOF

	print << x 10;		# null identifier is delimiter
    Merry Christmas!

	print <<`EOC`;		# execute commands
    echo hi there
    echo lo there
    EOC

	print <<"foo", <<"bar";	# you can stack them
    I said foo.
    foo
    I said bar.
    bar

=head2 List literals

List values are denoted by separating individual values by commas
(and enclosing the list in parentheses where precedence requires it):

    (LIST)

In a context not requiring an list value, the value of the list
literal is the value of the final element, as in the C comma operator.
For example,

    @foo = ('cc', '-E', $bar);

assigns the entire list value to array foo, but

    $foo = ('cc', '-E', $bar);

assigns the value of variable bar to variable foo.  Note that the value
of an actual array in a scalar context is the length of the array; the
following assigns to $foo the value 3:

    @foo = ('cc', '-E', $bar);
    $foo = @foo;		# $foo gets 3

You may have an optional comma before the closing parenthesis of an
list literal, so that you can say:

    @foo = (
	1,
	2,
	3,
    );

When a LIST is evaluated, each element of the list is evaluated in a
list context, and the resulting list value is interpolated into LIST
just as if each individual element were a member of LIST.  Thus arrays
lose their identity in a LIST--the list

    (@foo,@bar,&SomeSub)

contains all the elements of @foo followed by all the elements of @bar,
followed by all the elements returned by the subroutine named SomeSub.

A list value may also be subscripted like a normal array.  Examples:

    $time = (stat($file))[8];	# stat returns array value
    $digit = ('a','b','c','d','e','f')[$digit-10];
    return (pop(@foo),pop(@foo))[0];

Lists may be assigned to if and only if each element of the list
is a legal lvalue:

    ($a, $b, $c) = (1, 2, 3);

    ($map{'red'}, $map{'blue'}, $map{'green'}) = (0x00f, 0x0f0, 0xf00);

The final element may be an array or a hash:

    ($a, $b, @rest) = split;
    local($a, $b, %rest) = @_;

You can actually put an array anywhere in the list, but the first array
in the list will soak up all the values, and anything after it will get
a null value.  This may be useful in a local() or my().

A hash literal contains pairs of values to be interpreted
as a key and a value:

    # same as map assignment above
    %map = ('red',0x00f,'blue',0x0f0,'green',0xf00);

It's often much more readable to use the => operator, which is nothing
more than a synonym for comma:

    %map = ('red'   => 0x00f,
	    'blue'  => 0x0f0,
	    'green' => 0xf00);

Array assignment in a scalar context returns the number of elements
produced by the expression on the right side of the assignment:

    $x = (($foo,$bar) = (3,2,1));	# set $x to 3, not 2

This is very handy when you want to do a list assignment in a Boolean
context, since most list functions return a null list when finished.
